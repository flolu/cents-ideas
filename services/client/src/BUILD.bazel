load("@build_bazel_rules_nodejs//:index.bzl", "nodejs_binary", "pkg_web")
load("@npm_bazel_rollup//:index.bzl", "rollup_bundle")
load("@npm_bazel_terser//:index.bzl", "terser_minified")
load("@npm//history-server:index.bzl", "history_server")
load("@npm_bazel_typescript//:index.bzl", "ts_devserver", "ts_library")
load("@k8s_deploy//:defaults.bzl", "k8s_deploy")
load("//services/client:ngsw_config.bzl", "ngsw_config")
load("@io_bazel_rules_docker//nodejs:image.bzl", "nodejs_image")
load("@npm//html-insert-assets:index.bzl", "html_insert_assets")
load("@io_bazel_rules_sass//:defs.bzl", "sass_binary")

package(default_visibility = ["//:__subpackages__"])

filegroup(
    name = "rxjs_umd_modules",
    srcs = [
        ":rxjs_shims.js",
        "@npm//:node_modules/rxjs/bundles/rxjs.umd.js",
    ],
)

sass_binary(
    name = "styles",
    src = "styles.sass",
)

ts_library(
    name = "initialize_testbed",
    testonly = True,
    srcs = ["initialize_testbed.ts"],
    deps = [
        "@npm//@angular/core",
        "@npm//@angular/platform-browser-dynamic",
    ],
)

ts_library(
    name = "dev_src",
    srcs = ["main.dev.ts"],
    tsconfig = "//services/client:tsconfig.json",
    use_angular_plugin = True,
    deps = [
        "//services/client/src/app:dev_app",
        "@npm//@angular/compiler-cli",
        "@npm//@angular/platform-browser",
    ],
)

html_insert_assets(
    name = "inject_scripts_for_dev",
    outs = ["index.html"],
    args = [
        "--html=$(execpath //services/client/src:index.dev.html)",
        "--out=$@",
        "--roots=. $(RULEDIR)",
        "--assets",
    ] + [
        "$(execpath %s)" % ":styles.css",
        "$(execpath %s)" % "@npm//:node_modules/zone.js/dist/zone.min.js",
        "./_/ts_scripts.js",
    ],
    data = [
        ":index.dev.html",
        ":manifest.webmanifest",
        ":styles.css",
        "@npm//:node_modules/zone.js/dist/zone.min.js",
    ],
)

ts_devserver(
    name = "live_server",
    entry_module = "cents_ideas/services/client/src/main.dev",
    scripts = [
        "@npm//:node_modules/tslib/tslib.js",
        ":rxjs_umd_modules",
    ],
    static_files = [
        ":inject_scripts_for_dev",
        ":manifest.webmanifest",
        ":styles.css",
        "@npm//:node_modules/zone.js/dist/zone.min.js",
        "//services/client/src/assets",
    ],
    deps = [":dev_src"],
)

ts_library(
    name = "prod_src",
    srcs = ["main.prod.ts"],
    tsconfig = "//services/client:tsconfig.json",
    use_angular_plugin = True,
    deps = [
        "//services/client/src/app:prod_app",
        "@npm//@angular/core",
        "@npm//@angular/platform-browser",
    ],
)

rollup_bundle(
    name = "prod_bundle_es2015",
    config_file = "rollup.config.js",
    entry_points = {
        ":main.prod.ts": "index.es2015",
    },
    output_dir = True,
    deps = [
        ":prod_src",
        "@npm//rollup-plugin-commonjs",
        "@npm//rollup-plugin-node-resolve",
    ],
)

terser_minified(
    name = "prod_bundle_es2015.min",
    src = ":prod_bundle_es2015",
)

html_insert_assets(
    name = "inject_scripts_for_prod",
    outs = ["_prod/index.html"],
    args = [
        "--html=$(execpath :index.prod.html)",
        "--out=$@",
        "--roots=. $(RULEDIR)",
        "--assets",
    ] + [
        "$(execpath %s)" % ":styles.css",
        "$(execpath %s)" % "@npm//:node_modules/zone.js/dist/zone.min.js",
        "$(execpath %s)/index.es2015.js" % ":prod_bundle_es2015.min",
    ],
    data = [
        ":index.prod.html",
        ":manifest.webmanifest",
        ":prod_bundle_es2015.min",
        ":styles.css",
        "@npm//:node_modules/zone.js/dist/zone.min.js",
    ],
)

pkg_web(
    name = "prod_pwa_app",
    srcs = [
        ":inject_scripts_for_prod",
        ":manifest.webmanifest",
        ":prod_bundle_es2015.min",
        ":robots.txt",
        ":styles.css",
        "//services/client/src/assets",
        "@npm//:node_modules/@angular/service-worker/ngsw-worker.js",
        "@npm//:node_modules/zone.js/dist/zone.min.js",
    ],
    additional_root_paths = ["npm/node_modules/@angular/service-worker"],
)

ngsw_config(
    name = "prod_pwa",
    src = ":prod_pwa_app",
    config = "//services/client:ngsw-config.json",
    index_html = ":inject_scripts_for_prod",
)

history_server(
    name = "pwa_server",
    data = [":prod_pwa"],
    templated_args = [
        "-a",
        "$(rlocation cents_ideas/services/client/src/prod_pwa)",
    ],
)

ts_library(
    name = "prod_server_lib",
    srcs = ["server.ts"],
    deps = [
        "//services/client/src/app:server",
        "@npm//@angular/core",
        "@npm//@nguniversal/express-engine",
        "@npm//@types/compression",
        "@npm//@types/express",
        "@npm//@types/node",
        "@npm//@types/yargs",
        "@npm//compression",
        "@npm//express",
        "@npm//yargs",
    ],
)

nodejs_binary(
    name = "prod_server",
    data = [
        ":prod_pwa",
        ":prod_server_lib",
    ],
    entry_point = ":server.ts",
)

nodejs_image(
    name = "prod_server_image",
    data = [
        ":prod_pwa",
        ":prod_server_lib",
    ],
    entry_point = ":server.prod.ts",
)

k8s_deploy(
    name = "k8s",
    images = {"client:placeholder_name": ":prod_server_image"},
    template = ":client.yaml",
)
