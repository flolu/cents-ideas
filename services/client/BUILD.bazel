load("@build_bazel_rules_nodejs//:index.bzl", "nodejs_binary")
load("@npm_bazel_rollup//:index.bzl", "rollup_bundle")
load("@npm_bazel_terser//:index.bzl", "terser_minified")
load("@npm//history-server:index.bzl", "history_server")
load("@npm_bazel_typescript//:index.bzl", "ts_devserver", "ts_library")
load("@k8s_deploy//:defaults.bzl", "k8s_deploy")
load("@microk8s_deploy//:defaults.bzl", "microk8s_deploy")
load("@io_bazel_rules_docker//nodejs:image.bzl", "nodejs_image")
load("@io_bazel_rules_sass//:defs.bzl", "sass_binary")
load("//packages/angular-bazel:tools/angular_ts_library.bzl", "ng_ts_library")
load("//packages/angular-bazel:tools/pkg_pwa.bzl", "pkg_pwa")
load("//packages/angular-bazel:tools/insert_html_assets.bzl", "insert_html_assets")

package(default_visibility = ["//visibility:public"])

exports_files([
    "ngsw-config.json",
    "tsconfig.json",
])

sass_binary(
    name = "styles",
    src = "styles.sass",
)

ts_library(
    name = "initialize_testbed",
    testonly = True,
    srcs = ["initialize_testbed.ts"],
    deps = [
        "@npm//@angular/core",
        "@npm//@angular/platform-browser-dynamic",
    ],
)

ng_ts_library(
    name = "dev_src",
    srcs = ["main.dev.ts"],
    tsconfig = "//services/client:tsconfig.json",
    deps = [
        "//services/client/app:dev",
        "@npm//@angular/platform-browser",
    ],
)

ts_devserver(
    name = "dev_server",
    additional_root_paths = ["centsideas/services/client/_"],
    entry_module = "centsideas/services/client/main.dev",
    port = 4200,
    scripts = [
        "@npm//:node_modules/tslib/tslib.js",
        "//packages/angular-bazel:rxjs_umd_modules",
    ],
    serving_path = "/bundle.min/index.es2015.js",
    static_files = [
        "@npm//:node_modules/zone.js/dist/zone.min.js",
        "//services/client/assets",
        ":asset_injected_index_html",
        ":manifest.webmanifest",
        ":favicon.ico",
        ":styles.css",
    ],
    deps = [":dev_src"],
)

ng_ts_library(
    name = "prod_src",
    srcs = ["main.prod.ts"],
    tsconfig = "//services/client:tsconfig.json",
    deps = [
        "//services/client/app:prod",
        "@npm//@angular/core",
        "@npm//@angular/platform-browser",
    ],
)

rollup_bundle(
    name = "bundle",
    config_file = "//packages/angular-bazel:rollup.config.js",
    entry_points = {
        ":main.prod.ts": "index.es2015",
    },
    output_dir = True,
    deps = [
        ":prod_src",
        "@npm//rollup-plugin-commonjs",
        "@npm//rollup-plugin-node-resolve",
    ],
)

terser_minified(
    name = "bundle.min",
    src = ":bundle",
    config_file = "//packages/angular-bazel:terser.config.json",
)

insert_html_assets(
    name = "asset_injected_index_html",
    outs = ["_/index.html"],
    asset_paths = [
        "$(execpath styles.css)",
        "$(execpath @npm//:node_modules/zone.js/dist/zone.min.js)",
        "$(execpath bundle.min)/index.es2015.js",
    ],
    data = [
        ":bundle.min",
        ":index.html",
        ":styles.css",
        "@npm//:node_modules/zone.js/dist/zone.min.js",
    ],
    html_file = ":index.html",
)

pkg_pwa(
    name = "pwa",
    srcs = [
        ":asset_injected_index_html",
        ":bundle.min",
        ":favicon.ico",
        ":manifest.webmanifest",
        ":robots.txt",
        ":styles.css",
        "//services/client/assets",
    ],
    index_html = ":asset_injected_index_html",
    ngsw_config = ":ngsw-config.json",
)

history_server(
    name = "pwa_server",
    data = [":pwa"],
    templated_args = ["-a $$(rlocation $(rootpath :pwa))"],
)

ts_library(
    name = "server_lib",
    srcs = ["server.ts"],
    deps = [
        "//services/client/app:server",
        "@npm//@angular/core",
        "@npm//@nguniversal/express-engine",
        "@npm//@types/cookie-parser",
        "@npm//@types/express",
        "@npm//@types/node",
        "@npm//@types/yargs",
        "@npm//cookie-parser",
        "@npm//express",
        "@npm//yargs",
    ],
)

nodejs_binary(
    name = "client",
    data = [
        ":pwa",
        ":server_lib",
    ],
    entry_point = ":server.ts",
)

nodejs_image(
    name = "prod_server_image",
    data = [
        ":pwa",
        ":server_lib",
    ],
    entry_point = ":server.ts",
)

k8s_deploy(
    name = "k8s",
    images = {"client:placeholder_name": ":prod_server_image"},
    template = ":client.yaml",
)

microk8s_deploy(
    name = "microk8s",
    images = {"client:placeholder_name": ":prod_server_image"},
    template = ":client.yaml",
)
